buildscript {
    ext {
        kotlinVersion = "1.2.51"
        coroutineVersion = "0.22.3"
        arrowVersion = "0.6.1"
        commonsIOVersion = '2.5'

        jUnitVersion = "5.1.0"
        spekVersion = "1.1.5"
        bintrayGradleVersion = "1.7"
        kluentVersion = "1.35"
        logbackVersion = '1.2.3'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:${bintrayGradleVersion}"
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.51"
    id "org.jetbrains.kotlin.kapt" version "1.2.51"
    id "io.gitlab.arturbosch.detekt" version "1.0.0.RC8"
}

apply plugin: "jacoco"
apply plugin: "idea"
apply plugin: 'kotlin'
apply plugin: 'com.jfrog.bintray'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}


jacoco {
    toolVersion = "0.8.0"
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.destination "${buildDir}/reports/coverage"
    }
}

check.dependsOn jacocoTestReport

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutineVersion"
    compile "commons-io:commons-io:${commonsIOVersion}"

    compile "ch.qos.logback:logback-core:$logbackVersion"
    compile "ch.qos.logback:logback-classic:$logbackVersion"

    testCompile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
    testCompile "org.amshove.kluent:kluent:$kluentVersion"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$jUnitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$jUnitVersion"

    testCompile("org.jetbrains.spek:spek-api:$spekVersion") {
        exclude group: 'org.jetbrains.kotlin'
    }
    testRuntime("org.jetbrains.spek:spek-junit-platform-engine:$spekVersion") {
        exclude group: 'org.jetbrains.kotlin'
    }
}

detekt {
    version = "1.0.0.RC8"
    profile("main") {
        input = "$projectDir/src/main/kotlin"
        config = "$rootDir/detekt.yml"
        filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
    }
}

test {
    finalizedBy detektCheck
    finalizedBy jacocoTestReport

    useJUnitPlatform {
        includeEngines 'spek'
    }

    testLogging {
        events "passed", "skipped", "failed"
    }
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

compileKotlin {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    kotlinOptions {
        jvmTarget = "1.8"
        apiVersion = "1.2"
        languageVersion = "1.2"
        allWarningsAsErrors = true
    }
}

compileTestKotlin {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    kotlinOptions {
        jvmTarget = "1.8"
        apiVersion = "1.2"
        languageVersion = "1.2"
        allWarningsAsErrors = true
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

artifacts {
    archives sourcesJar
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true

        sourceDirs += file('build/generated/source/kapt/main')
        testSourceDirs += file('build/generated/source/kapt/test')

        sourceDirs += file('build/generated/source/kaptKotlin/main')
        testSourceDirs += file('build/generated/source/kaptKotlin/test')

        sourceDirs += file('build/tmp/kapt/main/kotlinGenerated')
        testSourceDirs += file('build/tmp/kapt/test/kotlinGeneratedst')
    }
}



bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    publish = true
    pkg {
        userOrg = 'kotlins'
        repo = 'org.kotlins'
        name = 'funpdf'
        desc = "Wkhtmltopdf Wrapper for Kotlin."
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/jasoet/funpdf.git'
        issueTrackerUrl = 'https://github.com/jasoet/funpdf/issues'
        labels = ['kotlin', 'pdf', 'wkthmltopdf']
        publicDownloadNumbers = true
        override = true
        version {
            name = "${project.version}"
            desc = "funpdf version ${project.version}, Need More Test!."
            vcsTag = "${project.version}"
        }
    }
}

task compile(dependsOn: "testClasses")

wrapper {
    gradleVersion = '4.9'
}
